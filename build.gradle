plugins {
    id "java"
    id "net.minecrell.licenser" version "0.4.1"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}

group "me.shedaniel"
archivesBaseName = rootProject.name
version = "1.0." + (System.getenv("GITHUB_RUN_NUMBER") == null ? "9999" : System.getenv("GITHUB_RUN_NUMBER"))
sourceCompatibility = targetCompatibility = 1.8

license {
    include "**/*.java"
    header = project.file("HEADER.txt")
    ext {
        name = "shedaniel"
        year = "2021"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_KEY')
    publications = ["mavenJava"]
    publish = true
    pkg {
        repo = "cloth"
        name = "architectury-annotations"
        userOrg = "shedaniel"
        licenses = ["Apache-2.0"]
        version {
            vcsUrl = "https://github.com/architectury/architectury-annotations.git"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
